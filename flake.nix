{
  description = "A very basic flake";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";

    crane.url = "github:ipetkov/crane";

    flake-utils.url = "github:numtide/flake-utils";

    advisory-db = {
      url = "github:rustsec/advisory-db";
      flake = false;
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      crane,
      flake-utils,
      ...
    }:
    flake-utils.lib.eachDefaultSystem (
      system:

      let
        pkgs = nixpkgs.legacyPackages.${system};
        inherit (pkgs) lib;
        qtEnv =
          with pkgs.qt6;
          env "qt-custom-${qtbase.version}" [
            qtdeclarative
            qtbase
            pkgs.kdePackages.layer-shell-qt
            pkgs.kdePackages.qtimageformats
          ];

        craneLib = crane.mkLib pkgs;
        commonArgs = {
          src = craneLib.cleanCargoSource ./.;
          strictDeps = true;

          nativeBuildInputs = with pkgs; [
            pkg-config
            cmake
            qtEnv
            binutils
            clang-tools
            clang
            lld
            libxcb
            makeWrapper
          ];

          buildInputs = with pkgs; [
            libxkbcommon
            qtEnv
            libxcb
            libglvnd
            libx11
          ];
        };

        src = lib.cleanSource ./.;
        cargoArtifacts = craneLib.buildDepsOnly commonArgs;
        individualCrateArgs = commonArgs // {
          inherit cargoArtifacts;
          inherit (craneLib.crateNameFromCargoToml { inherit src; }) version;
        };

        gifboard = craneLib.buildPackage (
          individualCrateArgs
          // {
            pname = "gifboard";
            src = lib.cleanSource ./.;

            preCheck = ''
              export LD_LIBRARY_PATH="${
                with pkgs;
                lib.makeLibraryPath [
                  qtEnv
                  libxkbcommon
                  libglvnd
                  libxcb
                  stdenv.cc.cc.lib
                ]
              }:$LD_LIBRARY_PATH"
            '';

            postInstall = ''
              wrapProgram $out/bin/gifboard \
                --prefix LD_LIBRARY_PATH : "${
                  with pkgs;
                  lib.makeLibraryPath [
                    libxkbcommon
                    qtEnv
                    libglvnd
                    libxcb
                    stdenv.cc.cc.lib
                  ]
                }"
            '';

            meta = {
              mainProgram = "gifboard";
              license = pkgs.lib.licenses.gpl3;
              homepage = "https://github.com/Kaisia-Estrel/gifboard";
            };
          }
        );
      in
      {
        packages.gifboard = gifboard;
        packages.default = self.packages.${system}.gifboard;

        apps.gifboard = flake-utils.lib.mkApp {
          drv = gifboard;
        };
        apps.default = self.apps.${system}.gifboard;

        devShells.default = craneLib.devShell {

          QMAKE = "${qtEnv}/bin/qmake";
          hardeningDisable = [ "fortify" ];
          RUSTC_WRAPPER = "${pkgs.sccache}/bin/sccache";

          LD_LIBRARY_PATH = lib.makeLibraryPath [
            pkgs.stdenv.cc.cc.lib
            qtEnv
            pkgs.libxkbcommon
            pkgs.libglvnd
            pkgs.libxcb

            pkgs.zlib
            pkgs.libx11
            pkgs.fontconfig.lib
            pkgs.harfbuzz
            pkgs.freetype
            pkgs.libsm
            pkgs.libice
          ];

          packages = [
            pkgs.pkg-config
            pkgs.binutils
            pkgs.clang-tools
            pkgs.cmake
            pkgs.clang
            pkgs.rustPackages.cargo
            pkgs.rustPackages.clippy
            pkgs.rustPackages.rustc
            pkgs.rustPackages.rustfmt
            pkgs.rust-analyzer
            pkgs.lld
            pkgs.ninja
            pkgs.sccache
            pkgs.xdpyinfo
            pkgs.dwm

            pkgs.linuxdeploy
            pkgs.linuxdeploy-plugin-qt
          ];

          inherit (commonArgs) buildInputs nativeBuildInputs;
        };
      }
    );
}
